buildscript {
    ext {
        detektVersion = "1.+"
        kotlinVersion = "1.+"
        ktlintGradleVersion = "9.3.+"
        ktlintVersion = "0.38.1"
        junitVersion = "5.+"
        kluentVersion = "1.+"
    }

    repositories {
        gradlePluginPortal()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
        classpath "io.gitlab.arturbosch.detekt:detekt-gradle-plugin:$detektVersion"
        classpath "org.jlleitschuh.gradle:ktlint-gradle:$ktlintGradleVersion"
    }

    configurations.classpath {
        resolutionStrategy.activateDependencyLocking()
    }
}

apply plugin: 'kotlin'
apply plugin: "io.gitlab.arturbosch.detekt"
apply plugin: "org.jlleitschuh.gradle.ktlint"

repositories {
    mavenCentral()
    jcenter()
}

group = "de.smartsquare"
version = "0.1.0"

dependencyLocking {
    lockAllConfigurations()
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib"

    testImplementation "org.junit.jupiter:junit-jupiter:$junitVersion"
    testImplementation "org.amshove.kluent:kluent:$kluentVersion"
}

test {
    useJUnitPlatform()
}

detekt {
    config = files("${project.rootDir}/detekt.yml")

    buildUponDefaultConfig = true

    reports {
        xml.destination = file("$buildDir/reports/detekt/detekt.xml")
        html.destination = file("$buildDir/reports/detekt/detekt.html")
    }
}

ktlint {
    version = ktlintVersion
    reporters {
        reporter "checkstyle"
    }
}
